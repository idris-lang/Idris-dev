\section{\Idris{} --- the High Level Language}

\label{sect:hll}

\Idris{} is
a pure functional programming language with dependent types. It is
eagerly evaluated by default, and compiled via the Epic supercombinator
library~\cite{brady2011epic}, with irrelevant values and proof terms
automatically erased~\cite{Brady2003,Brady2005}.
In this section, I present some small examples to illustrate
the features of \Idris{}.
A full tutorial is available elsewhere \cite{idristutorial}. 

\subsection{Preliminaries}

\Idris{} defines several primitive types: fixed width integers
\tTC{Int}, arbitrary width integers \tTC{Integer}, and
\tTC{Float} for numeric operations, \tTC{Char} and \tTC{String} for
text manipulation, and \tTC{Ptr} which represents foreign pointers.
There are also several data types declared in the library, including
\tTC{Bool}, with values \tDC{True} and \tDC{False}. All of the usual
arithmetic and comparison operators are defined for the primitive types.

An \Idris{} program consists of a module declaration, followed by an optional
list of imports and a collection of definitions and declarations, for example:

\begin{SaveVerbatim}{constprims}

module Main

x : Int
x = 42

main : IO ()
main = putStrLn ("The answer is " ++ show x)

\end{SaveVerbatim}
\useverb{constprims}

\noindent
Like Haskell, the main function is called \texttt{main}, and input and output
is managed with an \texttt{IO} monad. Unlike Haskell, however, \remph{all} top
level functions must have a type signature. This is due to type inference
being, in general, undecidable for languages with dependent types.

A module declaration also opens a \remph{namespace}. The fully qualified names
declared in this module are \texttt{Main.x} and \texttt{Main.main}.

\subsection{Types and Functions}

Data types may be declared in a similar way to Haskell data types, with a
similar syntax. For example, Peano natural numbers and lists
are respectively declared in the library as follows:

\begin{SaveVerbatim}{natdecl}

data Nat    = Z   | S Nat 
data List a = Nil | (::) a (List a)

\end{SaveVerbatim}
\useverb{natdecl}

\noindent
A standard example of a \remph{dependent} type is the type of ``lists with
length'', conventionally called ``vectors'' in the dependently typed
programming literature. In \Idris{}, vectors are declared as follows:

\begin{SaveVerbatim}{vectdecl}

data Vect : Nat -> Type -> Type where
   Nil  : Vect Z a
   (::) : a -> Vect k a -> Vect (S k) a

\end{SaveVerbatim}
\useverb{vectdecl}

\noindent
The above declaration creates a family of types. The syntax resembles a Haskell
GADT declaration: it explicitly states the type
of the type constructor \tTC{Vect} --- it takes a \tTC{Nat} and a type as
arguments, where \tTC{Type} stands for the type of types. We say that \tTC{Vect}
is \emph{indexed} over \tTC{Nat} and \emph{parameterised} by a type. 
The distinction between parameters and indices is that a parameter is fixed
across an entire data structure, whereas an index may vary.
Note that constructor names may be overloaded (such as \texttt{Nil} and
\texttt{(::)} here) provided that they are declared in separate namespaces.

Functions are implemented by pattern matching. For example, the following
function defines concatenation of vectors, expressing in the type that
the resulting vector's length is the sum of the input lengths:

\begin{SaveVerbatim}{vapp}

(++) : Vect n a -> Vect m a -> Vect (n + m) a
Nil       ++ ys = ys
(x :: xs) ++ ys = x :: xs ++ ys

\end{SaveVerbatim}
\useverb{vapp}

\noindent
Functions can also be defined \emph{locally} using \texttt{where} clauses. For
example, to define a function which reverses a list, we can use an auxiliary
function which accumulates the new, reversed list, and which does not need to
be visible globally:

\begin{SaveVerbatim}{revwhere}

reverse : List a -> List a
reverse xs = revAcc [] xs where
  revAcc : List a -> List a -> List a
  revAcc acc [] = acc
  revAcc acc (x :: xs) = revAcc (x :: acc) xs

\end{SaveVerbatim}
\useverb{revwhere}

\noindent
Internally, \Idris{} programs are checked for \emph{totality} --- that they
produce an answer in finite time for all possible inputs --- but they are not
\emph{required} to be total by default. Totality checking serves two
purposes: firstly, if a program terminates for all inputs 
then its type gives a strong guarantee about the properties specified by its
type; secondly, we can optimise total programs more
aggressively~\cite{Brady2003}. 

Totality checking can be enforced by using the \texttt{total} keyword.
For example, the following definition is accepted:

\begin{SaveVerbatim}{vadd}

total vAdd : Num a => Vect n a -> Vect n a -> Vect n a
vAdd []        []        = []
vAdd (x :: xs) (y :: ys) = x + y :: vAdd xs ys

\end{SaveVerbatim}
\useverb{vadd}

\noindent
The elaborator can verify that this is total by checking that it covers all
possible patterns --- in this case, both arguments must be of the same form
as the type requires that the input vectors are the same length --- and that
recursive calls are on structurally smaller values. 

\Idris{} also supports coinductive definitions, though this and further
details of totality checking are beyond the scope of this paper; the totality
checker is implemented independently of elaboration and type checking.

\subsection{Implicit Arguments}

In order to understand how \Idris{} elaborates to an underlying type theory,
it is important first to understand the notion of \remph{implicit} arguments
in types.
To illustrate this, consider the finite sets:

\begin{SaveVerbatim}{findecl}

data Fin : Nat -> Type where
      fZ : Fin (S k)
      fS : Fin k -> Fin (S k)

\end{SaveVerbatim}
\useverb{findecl}

\noindent
As the name suggests, these are sets with a finite number of elements.
The declaration gives
\tDC{fZ} as the zeroth element of a finite set with \texttt{S k} elements,
and \texttt{fS n} as the
\texttt{n+1}th element of a finite set with \texttt{S k} elements. 

\tTC{Fin} is indexed by \tTC{Nat}, which represents the number of elements in
the set.  Neither constructor targets \texttt{Fin Z}, because the empty
set has no elements. The following function uses an element
of a finite set as a bounds-safe index into a vector:

\begin{SaveVerbatim}{vindex}

index : Fin n -> Vect n a -> a
index fZ     (x :: xs) = x
index (fS k) (x :: xs) = index k xs

\end{SaveVerbatim}
\useverb{vindex}

\noindent
Let us take a closer look at its type.
It takes two arguments, an element of the finite set of \texttt{n} elements, and a vector
with \texttt{n} elements of type \texttt{a}. But there are also two names, 
\texttt{n} and \texttt{a}, which are not declared explicitly. These are \emph{implicit}
arguments to \texttt{index}. The type of \texttt{index} could also be written as:

\begin{SaveVerbatim}{vindeximppl}

index : {a:_} -> {n:_} -> Fin n -> Vect n a -> a

\end{SaveVerbatim}
\useverb{vindeximppl}

\noindent
This gives bindings for \texttt{a} and \texttt{n} with placeholders for
their types, to be inferred by the machine. These types could also be given explicitly:

\begin{SaveVerbatim}{vindeximpty}

index : {a:Type} -> {n:Nat} -> Fin n -> Vect n a -> a

\end{SaveVerbatim}
\useverb{vindeximpty}

\noindent
Implicit arguments, given in braces \texttt{\{\}} in the type signature, are
not given in applications of \texttt{index}; their values can be inferred from
the types of the \texttt{Fin n} and \texttt{Vect n a} arguments. Any name which
appears as a parameter or index in a type signature, but which is otherwise
free, will be automatically bound as an implicit argument.  Implicit arguments
can still be given explicitly in applications, using the syntax
\texttt{\{a=value\}} and \texttt{\{n=value\}}, for example:

\begin{SaveVerbatim}{vindexexp}

index {a=Int} {n=2} fO (2 :: 3 :: Nil)

\end{SaveVerbatim}
\useverb{vindexexp}

\subsection{Classes}

\Idris{} supports overloading in two ways. Firstly, as we have already seen
with the constructors of \texttt{List} and \texttt{Vect}, names can be
overloaded in an ad-hoc manner and resolved according to the context in which
they are used. This is mostly for convenience, to eliminate the need to
decorate constructor names in similarly structured data types, and eliminate
explicit qualification of ambiguous names where only one is well-typed --- this
is especially useful for disambiguating record field names\footnote{Records are
however beyond the scope of this paper}.

Secondly, \Idris{} implements \remph{classes}, following Haskell's type
classes.  This allows a more principled approach to overloading --- a class
gives a collection of overloaded operations which describe the interface for
\remph{instances} of that class. \Idris{} classes follow Haskell~98 type
classes, except that multiple parameters are supported, and that classes can be
parameterised by \remph{any} value, not just types\footnote{Hence we refer to
them as \remph{classes} rather than \remph{type classes}.}.

A simple example is the \texttt{Show} class, which is defined in the
library and provides an interface for converting values to \texttt{String}s:

\begin{SaveVerbatim}{showclass}

class Show a where
    show : a -> String

\end{SaveVerbatim}
\useverb{showclass}

\noindent
An instance of a class is defined with an \texttt{instance} declaration, which
provides implementations of the function for a specific type. For example:

\begin{SaveVerbatim}{shownat}

instance Show Nat where
    show Z = "Z"
    show (S k) = "s" ++ show k

\end{SaveVerbatim}
\useverb{shownat}

\noindent
Instance declarations can themselves have constraints. For example, to define a
\texttt{Show} instance for vectors, we need to know that there is a
\texttt{Show} instance for the element type, because we are going to use it to
convert each element to a \texttt{String}:

\begin{SaveVerbatim}{showvec}

instance Show a => Show (Vect n a) where
    show xs = "[" ++ show' xs ++ "]" where
        show' : Vect n a -> String
        show' Nil        = ""
        show' (x :: Nil) = show x
        show' (x :: xs)  = show x ++ ", " ++ show' xs

\end{SaveVerbatim}
\useverb{showvec}

\noindent
Classes can also be extended. A logical next step from an equality relation \texttt{Eq}
is to define an ordering relation \texttt{Ord}. We can define an \texttt{Ord} class
which inherits methods from \texttt{Eq} as well as defining some of its own:

\begin{SaveVerbatim}{ord}

data Ordering = LT | EQ | GT

\end{SaveVerbatim}
\useverb{ord} 

\begin{SaveVerbatim}{eqord}

class Eq a => Ord a where
    compare : a -> a -> Ordering
    (<) : a -> a -> Bool
    -- etc

\end{SaveVerbatim}
\useverb{eqord}

\subsection{Matching on intermediate values}

%\subsubsection{\texttt{let} bindings}
%
%Intermediate values can be calculated using \texttt{let} bindings:
%
%\begin{SaveVerbatim}{letb}
%
%mirror : List a -> List a
%mirror xs = let xs' = rev xs in
%                app xs xs'
%
%\end{SaveVerbatim}
%\useverb{letb} 
%
%\noindent
%Pattern matching is also supported in \texttt{let} bindings. For example, extracting
%fields from a record can be achieved as follows, as well as by pattern matching at the top level:
%
%\begin{SaveVerbatim}{letp}
%
%data Person = MkPerson String Int
%
%showPerson : Person -> String
%showPerson p = let MkPerson name age = p in
%                   name ++ " is " ++ show age ++ " years old"
%
%\end{SaveVerbatim}
%\useverb{letp} 

\subsubsection{\texttt{case} expressions}

Intermediate values of \emph{non-dependent} types can be inspected using a
\texttt{case} expression.  For example, \texttt{list\_lookup} looks up an index
in a list, returning \texttt{Nothing} if the index is out of bounds. This can
be used to write \texttt{lookup\_default}, which looks up an index and
returns a default value if the index is out of bounds:

\begin{SaveVerbatim}{listlookup}

lookup_default : Nat -> List a -> a -> a
lookup_default i xs def = case list_lookup i xs of
                              Nothing => def
                              Just x => x

\end{SaveVerbatim}
\useverb{listlookup} 

The \texttt{case} construct is intended for simple analysis of intermediate
expressions to avoid the need to write auxiliary functions.  It will
\emph{only} work if each branch \emph{matches} a value of the same type, and
\emph{returns} a value of the same type.

\subsubsection{The \texttt{with} rule}

Since types can depend on values, the form of some arguments can be determined
by the value of others. For example, if we were to write down the implicit
length arguments to \texttt{(++)}, we would see that the form of the length argument was
determined by whether the vector was empty or not:

\begin{SaveVerbatim}{appdep}

(++) : Vect n a -> Vect m a -> Vect (n + m) a
(++) {n=O}   []        [] = []
(++) {n=S k} (x :: xs) ys = x :: xs ++ ys

\end{SaveVerbatim}
\useverb{appdep}

\noindent
If \texttt{n} was a successor in the \texttt{[]} case, or zero in the 
\texttt{::} case, the definition
would not be well typed.

Often, matching is required on the result of an intermediate computation
with a dependent type.
\Idris{} provides a construct for this, the \texttt{with} rule, 
inspired by views in \Epigram~\cite{McBride2004a},
which takes account of the
fact that matching on a value in a dependently typed language can affect what
is known about the forms of other values. 

For example, a \texttt{Nat} is either even or odd. 
If it is even it will
be the sum of two equal \texttt{Nat}s. Otherwise, it is the sum of two equal \texttt{Nat}s 
plus one:

\begin{SaveVerbatim}{parity}

data Parity : Nat -> Type where
   even : Parity (n + n)
   odd  : Parity (S (n + n))

\end{SaveVerbatim}
\useverb{parity}

\noindent
We say \texttt{Parity} is a \emph{view} of \texttt{Nat}. 
It has a \emph{covering function} which tests whether
it is even or odd and constructs the predicate accordingly.

\begin{SaveVerbatim}{parityty}

parity : (n:Nat) -> Parity n

\end{SaveVerbatim}
\useverb{parityty}

\noindent
Using this, a function which converts a natural number to a list
of binary digits (least significant first) is written as follows, using the \texttt{with}
rule:

\begin{SaveVerbatim}{natToBin}

natToBin : Nat -> List Bool
natToBin O = Nil
natToBin k with (parity k)
   natToBin (j + j)     | even = False :: natToBin j
   natToBin (S (j + j)) | odd  = True  :: natToBin j

\end{SaveVerbatim}
\useverb{natToBin}

\noindent
The value of the result of \texttt{parity k} affects the form of \texttt{k}, 
because the result
of \texttt{parity k} depends on \texttt{k}. 
So, as well as the patterns for the result of the
intermediate computation (\texttt{even} and \texttt{odd}) right of the 
\texttt{$\mid$}, the definition also expresses how
the results affect the other patterns left of the $\mid$. Note that there is a
function in the patterns (\texttt{+}) and repeated occurrences of \texttt{j} --- 
this is allowed
because another argument has determined the form of these patterns. In general,
non-linear patterns are allowed \emph{only} when their value is forced by
some other argument.


